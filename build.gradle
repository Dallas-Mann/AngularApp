plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.3.1.RELEASE'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: '2.3.1.RELEASE'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

def frontendDir = "$projectDir/AngularAppFrontend"
sourceSets {
    main {
        java {
            srcDirs = ["$projectDir/src/main/java"]
        }
        resources {
            srcDirs = ["$frontendDir/dist", "$projectDir/src/main/resources"]
        }
    }
}

task installAngular(type:Exec) {
    workingDir "$frontendDir"
    inputs.dir "$frontendDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
        commandLine "npm.cmd", "install"
    }
    else {
        commandLine "npm", "install"
    }
}

task buildAngular(type:Exec) {
    dependsOn "installAngular"
    workingDir "$frontendDir"
    inputs.dir "$frontendDir"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
        commandLine "ng.cmd", "build"
    }
    else {
        commandLine "ng", "build"
    }
}

processResources {
    dependsOn "buildAngular"
}

bootJar {
    manifest {
        attributes 'Start-Class': 'com.application.AngularAppBackend'
    }
}
